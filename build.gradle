buildscript {
    repositories {
        mavenLocal()
       // maven { url = "https://maven.aliyun.com/nexus/content/groups/public/" }
        google()
        maven { url = 'https://maven.minecraftforge.net/' }
        maven {
            name = "sponge"
            url = "https://repo.spongepowered.org/maven"
        }

        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath("com.anatawa12.forge:ForgeGradle:2.3-1.0.+") { changing = true }
        classpath 'org.spongepowered:mixingradle:0.6-SNAPSHOT'
    }
}

apply plugin: "net.minecraftforge.gradle.forge"
apply plugin: 'org.spongepowered.mixin'

group = 'huige233'
version = '1.1.4-test230'

minecraft {
    version = project.forgeVersion
    runDir = "run"

    // the mappings can be changed at any time, and must be in the following format.
    // snapshot_YYYYMMDD   snapshot are built nightly.
    // stable_#            stables are built at the discretion of the MCP team.
    // Use non-default mappings at your own risk. they may not always work.
    // simply re-run your setup task after changing the mappings to update your workspace.
    mappings = project.mcpVersion
    // makeObfSourceJar = false // an Srg named sources jar is made by default. uncomment this to disable.
    def args = [
            "-Dmixin.hotSwap=true",
            "-Dmixin.debug.export=true",
            "-Dmixin.checks.interfaces=true",
    //        "-Dlegacy.debugClassLoading=true",
 //           "-Dlegacy.debugClassLoadingFiner=true",
//            "-Dlegacy.debugClassLoadingSave=true",
            "-Dfml.coreMods.load=huige233.transcend.asm.FMLLoadingPlugin"
    ]

    clientJvmArgs.addAll(args)
    serverJvmArgs.addAll(args)
}

repositories {
    mavenCentral()
    mavenLocal()
    maven { url = "https://maven.aliyun.com/nexus/content/groups/public/" }
    maven {
        name = "buildcraft"
        url = "https://mod-buildcraft.com/maven"
    }
    maven {
        url "https://maven.cleanroommc.com"
    }
    maven {
        name = "blamejared"
        url = "https://maven.blamejared.com"
    }
    maven {
        name = "CoFH Maven"
        url = "https://maven.covers1624.net"
    }
    maven {
        name = "thiakil"
        url = "http://maven.thiakil.com"
    }
    maven {
        name = "Progwml6 maven"
        url = "https://dvs1.progwml6.com/files/maven/"
    }
    maven { url "https://maven.hellfiredev.net" }
    maven {
        name = "ModMaven"
        url = "https://modmaven.k-4u.nl"
    }
    maven {
        name = "ChickenBones Maven"
        url = "http://chickenbones.net/maven/"
    }
    maven { url = "https://repo.spongepowered.org/maven" }
    maven {
        name 'DVS1 Maven FS'
        url 'http://dvs1.progwml6.com/files/maven'
    }
    maven { url = "http://maven.cil.li/" }
    maven { url = "https://maven.ellpeck.de/" }
    maven { url ="https://www.cursemaven.com"}
//    maven {
//        //Baubles
//        name 'thiakil'
//        url 'https://maven.thiakil.com'
//    }
    maven { url = "file:/${projectDir}/local-repo" }
}


mixin {
    add sourceSets.main, "mixins.transcend.refmap.json"
}

dependencies {
    implementation 'org.jetbrains:annotations:23.0.0'
    deobfCompile "mezz.jei:jei_${mc_version}:${jei_version}"
    deobfCompile "com.github.mcjty:mcjtylib:${mc_short}-${mcjtylib_version}"
    deobfCompile "vazkii.botania:Botania:${botania_version}"
    deobfCompile "vazkii.psi:Psi:${psi_version}"
    deobfCompile "vazkii.autoreglib:AutoRegLib:${arl_version}"
    deobfCompile "com.blamejared:MTLib:${mtlib_version}"
    deobfCompile "curse.maven:astralsorcery-${astralsorcery_version}"
    deobfCompile "curse.maven:bloodmagic-${bloodmagic_version_curse}"
    deobfCompile "curse.maven:thaumcraft-${thaumcraft_version}"
    deobfCompile "curse.maven:guideapi-${guideapi_version}"
    deobfCompile "curse.maven:mantle-${mantle_build}"
    deobfCompile "curse.maven:tconstruct-${tic_build}"
    deobfCompile "curse.maven:brandonscore-${bcore_version}"
    deobfCompile "curse.maven:draconicevolution-${de_version}"
    deobfCompile "curse.maven:redstoneflux-${rf_version}"
    deobfCompile "curse.maven:avaritia-${avaritia_version}"
    deobfCompile "curse.maven:conarm-${conarm_version}"
    deobfCompile("codechicken:CodeChickenLib:${mc_version}-${ccl_version}:universal") {
        exclude group: "mezz.jei"
    }
    //compile fileTree(dir:'libs',includes:['*jar'])
    deobfCompile "mods:Baubles:1.12-1.5.2"
    //deobfCompile "mods:industrialcraft-2:2.8.188-ex112"
    //implementation "mods:industrialcraft-2:2.8.188-ex112"
    //runtimeOnly "net.industrial-craft:industrialcraft-2:2.8.221-ex112"
    //compileOnly "net.industrial-craft:industrialcraft-2:2.8.221-ex112:dev"
    deobfCompile "mods:SlashBlade:OHHH"
    deobfCompile ("zone.rong:mixinbooter:4.2")
    runtime "curse.maven:bwmcore-${bwm_core}"
}

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include "mcmod.info"

        // replace version and mcversion
        expand "version": project.version, "mcversion": project.minecraft.version
    }

    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude "mcmod.info"
    }
}

sourceSets {
    main {
        output.resourcesDir = file('build/combined')
        java.outputDir = file('build/combined')
    }
}

jar {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    //from configurations.provided.asFileTree.files.collect {zipTree(it)}

    manifest {
        attributes([
                //"TweakClass": "huige233.transcend.asm.tweaker.Tweaker",
                "FMLCorePlugin": "huige233.transcend.asm.FMLLoadingPlugin",
                "FMLAT": "transcend_at.cfg",
                "MixinConfigs": "mixins.transcend.json",
                "FMLCorePluginContainsFMLMod": true
        ])
    }
}

tasks.genIntellijRuns.doFirst {
    def workspaceFile = file(".idea/workspace.xml")
    workspaceFile.parentFile.mkdirs()
    if (!workspaceFile.exists()) {
        workspaceFile.write("""\
        <?xml version="1.0" encoding="UTF-8"?>
        <project version="4">
            <component name="RunManager">
            </component>
        </project>
        """.stripIndent())
    } else {
        def workspace = new XmlParser().parse(workspaceFile)
        def runManager = workspace.component.findAll({ it.@name == "RunManager" })
        if (runManager.size() == 0) {
            // When IDEA first imported the project, the RunManager node didn't exist.
            workspace.appendNode("component", ["name": "RunManager"])
        } else {
            // The genIntellijRuns task doesn't delete old run configurations, this is bad.
            runManager.each {
                it.configuration.findAll({ it.@name == "Minecraft Client" || it.@name == "Minecraft Server" }).each { configuration ->
                    it.remove(configuration)
                }
            }
        }
        new PrintWriter(new FileWriter(workspaceFile)).withCloseable {
            new XmlNodePrinter(it).print(workspace)
        }
    }
}

tasks.genIntellijRuns.doLast {
    def workspaceFile = file(".idea/workspace.xml")
    def workspace = new XmlParser().parse(workspaceFile)
    workspace.component.findAll({ it.@name == "RunManager" }).each {
        it.configuration.findAll({ it.@name == "Minecraft Client" || it.@name == "Minecraft Server" }).each {
            // The module name specified in the run configuration generated by the genIntellijRuns task is wrong.
            // It uses underscores instead of dots.
            // https://github.com/MinecraftForge/ForgeGradle/blob/FG_2.3/src/main/java/net/minecraftforge/gradle/user/UserBasePlugin.java#L1325
            it.module.@name = project.idea.module.name + '.' + project.minecraft.runSourceSet.name
        }
    }
    new PrintWriter(new FileWriter(workspaceFile)).withCloseable {
        new XmlNodePrinter(it).print(workspace)
    }
}
import net.minecraftforge.gradle.tasks.DownloadAssetsTask
import sun.misc.SharedSecrets
import sun.misc.Unsafe

// initialize the constant pool and force to load strings so that strings can be replaced
void initConstantPool(Class<?> clazz) {
    if (clazz != null && !clazz.interface) {
        initConstantPool clazz.superclass

        def constantPool = SharedSecrets.javaLangAccess.getConstantPool clazz
        for (i in 0..constantPool.size) {
            try {
                constantPool.getStringAt i
            } catch (def ignored) {}
        }
    }
}

// replace string values in constant pools
void replaceString(String str1, String str2) {
    String.class.getDeclaredField "value" with {
        it.setAccessible true
        it.set str1, str2.toCharArray()
    }
}

// https://github.com/MinecraftForge/ForgeGradle/blob/FG_2.3/src/main/java/net/minecraftforge/gradle/tasks/DownloadAssetsTask.java#L189
initConstantPool Unsafe.theUnsafe.allocateInstance(DownloadAssetsTask.GetAssetTask.class).class
replaceString "http://resources.download.minecraft.net/", "https://resources.download.minecraft.net/"
replaceString "http://maven.ic2.player.to/", "https://maven.ic2.player.to/"
